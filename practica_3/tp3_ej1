{Bienvenidos al entorno CMRE.
Lo siguiente es un código de ejemplo que implementa un
proceso que recibe un número de avenida como parámetro,
se posiciona en esa avenida y la recorre.}

programa ejemplo
procesos

  proceso juntarFlores(ES flores: numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      flores := flores + 1
  fin
  
  proceso juntarPapeles(ES papeles: numero)
  comenzar
    mientras (HayPapelEnLaEsquina)
      tomarPapel
      papeles := papeles + 1
  fin
  
  proceso depositarFlores(E flores: numero)
  comenzar
    repetir flores
      depositarFlor
  fin
  
  proceso depositarPapeles(E papeles: numero)
  comenzar
    repetir papeles
      depositarPapel
  fin
  
  proceso esquinaAleatoria(E avIni: numero; E caIni: numero; E avFin: numero; E caFin: numero; ES av: numero; ES ca: numero)
  variables
    aleatorio: numero
  comenzar
    Random(aleatorio, avIni, avFin)
    av := aleatorio
    
    Random(aleatorio, caIni, caFin)
    ca := aleatorio
  fin
  
areas
  areaF: AreaPC(1, 1, 5, 10)
  areaP: AreaPC(6, 1, 10, 9)
  esqF1: AreaP(6, 10, 6, 10)
  esqF2: AreaP(7, 10, 7, 10)
  esqP1: AreaP(8, 10, 8, 10)
  esqP2: AreaP(9, 10, 9, 10)
  esqZ:  AreaC(10, 10, 10, 10)
robots

  robot florero
  variables
    flores, av, ca, x, y: numero
  comenzar
    flores := 0
    
    { Mi posicion actual }
    x := PosAv
    y := PosCa
       
    repetir 5
      { Genero la posicion aleatoria }
      esquinaAleatoria(1, 1, 5, 10, av, ca)
      
      { Bloqueo la esquina }
      BloquearEsquina(av, ca)
      
      { Me muevo a la esquina aleatoria }
      Pos(av, ca)
      
      juntarFlores(flores)
      
      { Vueve a casa }
      Pos(x, y)
      
      { Desbloqueo la esquina }
      LiberarEsquina(av, ca)
    
    { Deposito todo en la esquina final }  
    BloquearEsquina(10, 10)
    Pos(10, 10)
    depositarFlores(flores)
    Pos(x, y)
    LiberarEsquina(10, 10)
    
  fin
  
  robot papelero
  variables
    papeles, av, ca, x, y: numero
  comenzar
    papeles := 0
    
    { Mi posición actual }
    x := PosAv
    y := PosCa
    
    repetir 3
      esquinaAleatoria(6, 1, 10, 9, av, ca)
      BloquearEsquina(av, ca)
      Pos(av, ca)
      juntarPapeles(papeles)
      Pos(x, y)
      LiberarEsquina(av, ca)
      
    BloquearEsquina(10, 10)
    Pos(10, 10)
    depositarPapeles(papeles)
    Pos(x, y)
    LiberarEsquina(10, 10)
    
  fin

variables
  f1: florero
  f2: florero
  p1: papelero
  p2: papelero
comenzar

  AsignarArea(f1, areaF)
  AsignarArea(f2, areaF)
  AsignarArea(p1, areaP)
  AsignarArea(p2, areaP)
  AsignarArea(f1, esqF1)
  AsignarArea(f2, esqF2)
  AsignarArea(p1, esqP1)
  AsignarArea(p2, esqP2)
  AsignarArea(f1, esqZ)
  AsignarArea(f2, esqZ)
  AsignarArea(p1, esqZ)
  AsignarArea(p2, esqZ)
  
  Iniciar(f1, 6, 10)
  Iniciar(f2, 7, 10)
  Iniciar(p1, 8, 10)
  Iniciar(p2, 9, 10)

fin