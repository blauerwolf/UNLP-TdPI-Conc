{
  Implemente una variante en la cual los robots luego de tomar cada flor de la esquina
  (10,10) vuelvan a su esquina inicial, pasen a la esquina (11,11) a depositarla y finalmente
  vuelvan a la esquina inicial.
}

programa tp4_ej4b
procesos

  proceso juntarFlor(ES pudo: boolean)
  comenzar
    si (HayFlorEnLaEsquina)
      tomarFlor
      pudo := V
    sino
      pudo := F
  fin
  
  proceso juntarFlores
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
  fin
  
  proceso inicializarRobots
  comenzar
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    EnviarMensaje(3, r3)
    EnviarMensaje(4, r4)
  fin
  
areas
  areaFlores: AreaPC(10, 10, 11, 11)
  esqJ:  AreaP(1, 1, 1, 1) 
  esqR1: AreaP(9, 9, 9, 9)
  esqR2: AreaP(9, 10, 9, 10)
  esqR3: AreaP(9, 11, 9, 11)
  esqR4: AreaP(9, 12, 9, 12)
  
robots
  robot fiscalizador
  variables
    quien: numero
  comenzar
    inicializarRobots
    
    repetir 4
      RecibirMensaje(quien, *)
      
    Informar("El ultimo es", quien)
    Pos(11, 11)
    juntarFlores
  fin
  
  robot juntador
  variables
    quien, x, y: numero
    continuar, exito: boolean
  comenzar
    { Inicializo variables } 
    continuar := V
    exito := F
    x := PosAv
    y := PosCa
    
    RecibirMensaje(quien, jefe)
    
    mientras (continuar)
      BloquearEsquina(10, 10)
      Pos(10, 10)
      juntarFlor(exito)
      
      Pos(x, y)
      LiberarEsquina(10, 10)
      
      si (exito)
        BloquearEsquina(11, 11)
        Pos(11, 11)
        depositarFlor
        Pos(x, y)
        LiberarEsquina(11, 11)
      sino
        continuar := F
        EnviarMensaje(quien, jefe)      
  fin
  
variables
  jefe: fiscalizador
  r1: juntador
  r2: juntador
  r3: juntador
  r4: juntador
comenzar
  AsignarArea(r1, areaFlores)
  AsignarArea(r2, areaFlores)
  AsignarArea(r3, areaFlores)
  AsignarArea(r4, areaFlores)
  AsignarArea(jefe, areaFlores)
  AsignarArea(jefe, esqJ)
  AsignarArea(r1, esqR1)
  AsignarArea(r2, esqR2)
  AsignarArea(r3, esqR3)
  AsignarArea(r4, esqR4)
  
  Iniciar(r1, 9, 9)
  Iniciar(r2, 9, 10)
  Iniciar(r3, 9, 11)
  Iniciar(r4, 9, 12)
  Iniciar(jefe, 1, 1)
fin