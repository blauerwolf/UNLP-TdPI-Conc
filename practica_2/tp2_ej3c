{
  Analice (no es necesario implementar) cómo implementaría el inciso b si ahora cada
  equipo debe realizar 8 segmentos de 20 esquinas.
}
programa ejercicio3
procesos
  
  proceso juntarPapeles(ES papeles: numero)
  comenzar
    mientras (HayPapelEnLaEsquina)
      tomarPapel
      papeles := papeles + 1
  fin
  
  proceso juntarFlores(ES flores: numero)
  comenzar
    mientras (HayFlorEnLaEsquina)
      tomarFlor
      flores := flores + 1
  fin
  
  proceso recorrerCalleYJuntarPapeles(E avIni: numero; E caIni: numero; E pasos: numero; ES papeles: numero)
  comenzar
    Pos(avIni, caIni)

    repetir (pasos - 1)
      juntarPapeles(papeles)
      mover
     
    juntarPapeles(papeles)

  fin
  
  proceso recorrerCalleYJuntarFlores(E avIni: numero; E caIni: numero; E pasos: numero; ES flores: numero)
  comenzar
    Pos(avIni, caIni)    
    
    repetir (pasos - 1)
      juntarFlores(flores)
      mover
      
    juntarFlores(flores)
      
  fin
  
areas
  areaA11: AreaP(1, 1, 10, 1)
  areaA12: AreaP(21, 1, 30, 1)
  areaA21: AreaP(11, 1, 20, 1)
  areaA22: AreaP(31, 1, 40, 1)
  areaB11: AreaP(1, 5, 10, 5)
  areaB12: AreaP(21, 5, 30, 5)
  areaB21: AreaP(11, 5, 20, 5)
  areaB22: AreaP(31, 5, 40, 5)
robots
  robot teamA1
  variables
    papeles: numero
  comenzar
    papeles := 0
    derecha
    recorrerCalleYJuntarPapeles(1, 1, 10, papeles)
    EnviarMensaje(papeles, a2)
    RecibirMensaje(papeles, a2)
    recorrerCalleYJuntarPapeles(21, 1, 10, papeles)
    EnviarMensaje(papeles, a2)
  fin
  
  robot teamB1
  variables
    flores: numero
  comenzar
    flores := 0
    derecha
    recorrerCalleYJuntarFlores(1, 5, 10, flores)
    EnviarMensaje(flores, b2)
    RecibirMensaje(flores, b2)
    recorrerCalleYJuntarFlores(21, 5, 10, flores)
    EnviarMensaje(flores, b2)
  fin
  
  robot teamA2
  variables
    papeles: numero
  comenzar
    derecha
    RecibirMensaje(papeles, a1)
    recorrerCalleYJuntarPapeles(11, 1, 10, papeles)
    EnviarMensaje(papeles, a1)
    RecibirMensaje(papeles, a1)
    recorrerCalleYJuntarPapeles(31, 1, 10, papeles)
    Informar(papeles)
    
  fin
  
  robot teamB2
  variables
    flores: numero
  comenzar
    derecha
    RecibirMensaje(flores, b1)
    recorrerCalleYJuntarFlores(11, 5, 10, flores)
    EnviarMensaje(flores, b1)
    RecibirMensaje(flores, b1)
    recorrerCalleYJuntarFlores(31, 5, 10, flores)
    Informar(flores)
    
  fin
  
  
variables
  a1: teamA1
  a2: teamA2
  b1: teamB1
  b2: teamB2
comenzar
  AsignarArea(a1, areaA11)
  AsignarArea(a1, areaA12)
  AsignarArea(a2, areaA21)
  AsignarArea(a2, areaA22)
  AsignarArea(b1, areaB11)
  AsignarArea(b1, areaB12)
  AsignarArea(b2, areaB21)
  AsignarArea(b2, areaB22)
  
  Iniciar(a1, 1, 1)
  Iniciar(a2, 11, 1)
  Iniciar(b1, 1, 5)
  Iniciar(b2, 11, 5)
fin