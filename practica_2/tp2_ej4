{
Realice un programa donde 4 robots colaboren para recoger todas las flores de una
esquina indicada por un robot jefe, seleccionada de manera aleatoria dentro del cuadrante
(2,2) y (10,10). Para ello el jefe determina inicialmente una esquina y los robots deben
accederla, tomar de a una las flores y volver a su posición inicial. Cuando los robots
terminan el jefe deberá informar cuál de ellos logró recoger más flores.
Las esquinas de inicio de los robots deberán ser jefe (1,1) y robots (2,1), (3, 1), (4,1) y (5,1).
}

programa ejemplo
procesos

  proceso juntarFlor(ES pudo: boolean)
  comenzar
    si (HayFlorEnLaEsquina)
      tomarFlor
      pudo := V
    sino
      pudo := F
  fin

  proceso inicializarRobots
  comenzar
    EnviarMensaje(1, r1)
    EnviarMensaje(2, r2)
    EnviarMensaje(3, r3)
    EnviarMensaje(4, r4)
  fin
  
  proceso avisarEsquina(E av: numero; E ca: numero)
  comenzar
    EnviarMensaje(av, r1)
    EnviarMensaje(ca, r1)
    
    EnviarMensaje(av, r2)
    EnviarMensaje(ca, r2)
    
    EnviarMensaje(av, r3)
    EnviarMensaje(ca, r3)
    
    EnviarMensaje(av, r4)
    EnviarMensaje(ca, r4)
  fin

areas
  cuadrante: AreaPC(2,2,10,10)
  esqR1: AreaP(2,1,2,1)
  esqR2: AreaP(3,1,3,1)
  esqR3: AreaP(4,1,4,1)
  esqR4: AreaP(5,1,5,1)
  esqJ:  AreaP(1,1,1,1)
  
robots

  robot fiscalizador
  variables
    maxFlores, ganador, quien, flores: numero
    av, ca: numero
    
  comenzar
    inicializarRobots
    maxFlores := -1
    ganador := -1
    
    Random(av, 2, 10)
    Random(ca, 2, 10)
    
    avisarEsquina(av, ca)
    
    repetir 4
      RecibirMensaje(quien, *)
      si (quien = 1)
        RecibirMensaje(flores, r1)
      sino
        si (quien = 2)
          RecibirMensaje(flores, r2)
        sino
          si (quien = 3)
            RecibirMensaje(flores, r3)
          sino
            RecibirMensaje(flores, r4)
            
      si (flores > maxFlores)
        maxFlores := flores
        ganador := quien
        
    Informar(ganador) 
  fin
  
  robot juntador    
  variables
    av, ca, quien, x, y, flores: numero
    continuar, exito: boolean
  comenzar
    { Seteos iniciales de variables }
    continuar := V
    exito := F
    x := PosAv
    y := PosCa
    flores := 0
    
    { Identifico al robot }
    RecibirMensaje(quien, jefe)
    
    { Recibo en cuotas la posicion de la esquina }
    RecibirMensaje(av, jefe)
    RecibirMensaje(ca, jefe)
    
    mientras (continuar)    
      { Bloqueo la esquina }
      BloquearEsquina(av, ca)
      Pos(av, ca)
      
      { Junta flor }
      juntarFlor(exito)
       
      { Libero la esquina }
      Pos(x, y)
      LiberarEsquina(av, ca)
      
      { Si pudo juntar, cuento la flor }
      si (exito)
        flores := flores + 1
      { Sino, informo al jefe que se acabaron }  
      sino
        continuar := F
        
    EnviarMensaje(quien, jefe)
    EnviarMensaje(flores, jefe)
      
  fin
  

    
variables
  jefe: fiscalizador
  r1: juntador
  r2: juntador
  r3: juntador
  r4: juntador
  
comenzar
  AsignarArea(jefe, esqJ)
  AsignarArea(r1, cuadrante)
  AsignarArea(r2, cuadrante)
  AsignarArea(r3, cuadrante)
  AsignarArea(r4, cuadrante)
  AsignarArea(r1, esqR1)
  AsignarArea(r2, esqR2)
  AsignarArea(r3, esqR3)
  AsignarArea(r4, esqR4)
  AsignarArea(jefe, esqJ)
  Iniciar(r1, 2, 1)
  Iniciar(r2, 3, 1)
  Iniciar(r3, 4, 1)
  Iniciar(r4, 5, 1)
  Iniciar(jefe, 1, 1) 
fin